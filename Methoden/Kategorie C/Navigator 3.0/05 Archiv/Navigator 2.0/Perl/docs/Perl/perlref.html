<!-- $Id$ -->
<HTML><HEAD>
<CENTER><TITLE>perlref</TITLE>
</HEAD>
<BODY></CENTER><p><hr>

<H1> 
<A NAME="perlref_name_0">
NAME</A>
</H1>
perlref - Perl references and nested data structures
<p><p><hr>

<H1> 
<A NAME="perlref_description_0">
DESCRIPTION</A>
</H1>
Before release 5 of Perl it was difficult to represent complex data
structures, because all references had to be symbolic, and even that was
difficult to do when you wanted to refer to a variable rather than a
symbol table entry.  Perl 5 not only makes it easier to use symbolic
references to variables, but lets you have ``hard'' references to any piece
of data.  Any scalar may hold a hard reference.  Since arrays and hashes
contain scalars, you can now easily build arrays of arrays, arrays of
hashes, hashes of arrays, arrays of hashes of functions, and so on.
<p>Hard references are smart--they keep track of reference counts for you,
automatically freeing the thing referred to when its reference count
goes to zero.  (Note: The reference counts for values in self-referential
or cyclic data structures may not go to zero without a little help; see
<EM>``Two-Phased Garbage Collection''</EM> for a detailed explanation.
If that thing happens to be an object, the object is
destructed.  See the <EM>perlobj</EM>  manpage
 for more about objects.  (In a sense,
everything in Perl is an object, but we usually reserve the word for
references to objects that have been officially ``blessed'' into a class package.)
<p>A symbolic reference contains the name of a variable, just as a
symbolic link in the filesystem merely contains the name of a file.  
The <CODE>*glob</CODE> notation is a kind of symbolic reference.  Hard references
are more like hard links in the file system: merely another way
at getting at the same underlying object, irrespective of its name.
<p>``Hard'' references are easy to use in Perl.  There is just one
overriding principle:  Perl does no implicit referencing or
dereferencing.  When a scalar is holding a reference, it always behaves
as a scalar.  It doesn't magically start being an array or a hash
unless you tell it so explicitly by dereferencing it.
<p>References can be constructed several ways.
<p>
<OL>
<LI>By using the backslash operator on a variable, subroutine, or value.
(This works much like the &amp; (address-of) operator works in C.)  Note
that this typically creates <EM>ANOTHER</EM> reference to a variable, since
there's already a reference to the variable in the symbol table.  But
the symbol table reference might go away, and you'll still have the
reference that the backslash returned.  Here are some examples:
<p>
<XMP>
    $scalarref = \$foo;
    $arrayref  = \@ARGV;
    $hashref   = \%ENV;
    $coderef   = \&handler;
    $globref   = \*STDOUT;

</XMP>
<p>
<LI>A reference to an anonymous array can be constructed using square
brackets:
<p>
<XMP>
    $arrayref = [1, 2, ['a', 'b', 'c']];

</XMP>
<p>Here we've constructed a reference to an anonymous array of three elements
whose final element is itself reference to another anonymous array of three
elements.  (The multidimensional syntax described later can be used to
access this.  For example, after the above, <CODE><STRONG>$arrayref</STRONG>-&gt;[2][1]</CODE> would have
the value ``b''.)
<p>Note that taking a reference to an enumerated list is not the same
as using square brackets--instead it's the same as creating
a list of references!
<p>
<XMP>
    @list = (\$a, \@b, \%c);  
    @list = \($a, @b, %c);      # same thing!

</XMP>
<p>As a special case, <CODE>\(<STRONG>@foo</STRONG>)</CODE> returns a list of references to the contents 
of <CODE><STRONG>@foo</STRONG></CODE>, not a reference to <CODE><STRONG>@foo</STRONG></CODE> itself.  Likewise for <CODE><STRONG>%foo</STRONG></CODE>.
<p>
<LI>A reference to an anonymous hash can be constructed using curly
brackets:
<p>
<XMP>
    $hashref = {
        'Adam'  => 'Eve',
        'Clyde' => 'Bonnie',
    };

</XMP>
<p>Anonymous hash and array constructors can be intermixed freely to
produce as complicated a structure as you want.  The multidimensional
syntax described below works for these too.  The values above are
literals, but variables and expressions would work just as well, because
assignment operators in Perl (even within <EM>local()</EM> or <EM>my()</EM>) are executable
statements, not compile-time declarations.
<p>Because curly brackets (braces) are used for several other things
including BLOCKs, you may occasionally have to disambiguate braces at the
beginning of a statement by putting a <CODE>+</CODE> or a <CODE>return</CODE> in front so
that Perl realizes the opening brace isn't starting a BLOCK.  The economy and
mnemonic value of using curlies is deemed worth this occasional extra
hassle.
<p>For example, if you wanted a function to make a new hash and return a
reference to it, you have these options:
<p>
<XMP>
    sub hashem {        { @_ } }   # silently wrong
    sub hashem {       +{ @_ } }   # ok
    sub hashem { return { @_ } }   # ok

</XMP>
<p>
<LI>A reference to an anonymous subroutine can be constructed by using
<CODE>sub</CODE> without a subname:
<p>
<XMP>
    $coderef = sub { print "Boink!\n" };

</XMP>
<p>Note the presence of the semicolon.  Except for the fact that the code
inside isn't executed immediately, a <CODE>sub {}</CODE> is not so much a
declaration as it is an operator, like <CODE>do{}</CODE> or <CODE>eval{}</CODE>.  (However, no
matter how many times you execute that line (unless you're in an
<CODE>eval(``...'')</CODE>), <CODE><STRONG>$coderef</STRONG></CODE> will still have a reference to the <EM>SAME</EM>
anonymous subroutine.)
<p>Anonymous subroutines act as closures with respect to <EM>my()</EM> variables,
that is, variables visible lexically within the current scope.  Closure
is a notion out of the Lisp world that says if you define an anonymous
function in a particular lexical context, it pretends to run in that
context even when it's called outside of the context.
<p>In human terms, it's a funny way of passing arguments to a subroutine when
you define it as well as when you call it.  It's useful for setting up
little bits of code to run later, such as callbacks.  You can even
do object-oriented stuff with it, though Perl provides a different
mechanism to do that already--see the <EM>perlobj</EM>  manpage
.
<p>You can also think of closure as a way to write a subroutine template without
using eval.  (In fact, in version 5.000, eval was the <EM>only</EM> way to get
closures.  You may wish to use ``require 5.001'' if you use closures.)
<p>Here's a small example of how closures works:
<p>
<XMP>
    sub newprint {
        my $x = shift;
        return sub { my $y = shift; print "$x, $y!\n"; };
    }
    $h = newprint("Howdy");
    $g = newprint("Greetings");
    # Time passes...
    &$h("world");
    &$g("earthlings");

</XMP>
<p>This prints
<p>
<XMP>
    Howdy, world!
    Greetings, earthlings!

</XMP>
<p>Note particularly that <STRONG>$x</STRONG> continues to refer to the value passed into
<EM>newprint()</EM> <EM>despite</EM> the fact that the ``my <STRONG>$x</STRONG>'' has seemingly gone out of
scope by the time the anonymous subroutine runs.  That's what closure
is all about.
<p>This only applies to lexical variables, by the way.  Dynamic variables
continue to work as they have always worked.  Closure is not something
that most Perl programmers need trouble themselves about to begin with.
<p>
<LI>References are often returned by special subroutines called constructors.
Perl objects are just references to a special kind of object that happens to know
which package it's associated with.  Constructors are just special
subroutines that know how to create that association.  They do so by
starting with an ordinary reference, and it remains an ordinary reference
even while it's also being an object.  Constructors are customarily
named <EM>new()</EM>, but don't have to be:
<p>
<XMP>
    $objref = new Doggie (Tail => 'short', Ears => 'long');

</XMP>
<p>
<LI>References of the appropriate type can spring into existence if you
dereference them in a context that assumes they exist.  Since we haven't
talked about dereferencing yet, we can't show you any examples yet.
<p>
<LI>References to filehandles can be created by taking a reference to 
a typeglob.  This is currently the best way to pass filehandles into or
out of subroutines, or to store them in larger data structures.
<p>
<XMP>
    splutter(\*STDOUT);
    sub splutter {
        my $fh = shift;
        print $fh "her um well a hmmm\n";
    }
    $rec = get_rec(\*STDIN);
    sub get_rec {
        my $fh = shift;
        return scalar <$fh>;
    }

</XMP>
<p>
</OL>
That's it for creating references.  By now you're probably dying toknow how to use references to get back to your long-lost data.  There
are several basic methods.
<p>
<OL>
<LI>Anywhere you'd put an identifier (or chain of identifiers) as part
of a variable or subroutine name, you can replace the identifier with
a simple scalar variable containing a reference of the correct type:
<p>
<XMP>
    $bar = $$scalarref;
    push(@$arrayref, $filename);
    $$arrayref[0] = "January";
    $$hashref{"KEY"} = "VALUE";
    &$coderef(1,2,3);
    print $globref "output\n";

</XMP>
<p>It's important to understand that we are specifically <EM>NOT</EM> dereferencing
<CODE><STRONG>$arrayref</STRONG>[0]</CODE> or <CODE><STRONG>$hashref</STRONG>{``KEY''}</CODE> there.  The dereference of the
scalar variable happens <EM>BEFORE</EM> it does any key lookups.  Anything more
complicated than a simple scalar variable must use methods 2 or 3 below.
However, a ``simple scalar'' includes an identifier that itself uses method
1 recursively.  Therefore, the following prints ``howdy''.
<p>
<XMP>
    $refrefref = \\\"howdy";
    print $$$$refrefref;

</XMP>
<p>
<LI>Anywhere you'd put an identifier (or chain of identifiers) as part of a
variable or subroutine name, you can replace the identifier with a
BLOCK returning a reference of the correct type.  In other words, the
previous examples could be written like this:
<p>
<XMP>
    $bar = ${$scalarref};
    push(@{$arrayref}, $filename);
    ${$arrayref}[0] = "January";
    ${$hashref}{"KEY"} = "VALUE";
    &{$coderef}(1,2,3);
    $globref->print("output\n");  # iff you use FileHandle

</XMP>
<p>Admittedly, it's a little silly to use the curlies in this case, but
the BLOCK can contain any arbitrary expression, in particular,
subscripted expressions:
<p>
<XMP>
    &{ $dispatch{$index} }(1,2,3);      # call correct routine 

</XMP>
<p>Because of being able to omit the curlies for the simple case of <CODE><STRONG>$$</STRONG>x</CODE>,
people often make the mistake of viewing the dereferencing symbols as
proper operators, and wonder about their precedence.  If they were,
though, you could use parens instead of braces.  That's not the case.
Consider the difference below; case 0 is a short-hand version of case 1,
<EM>NOT</EM> case 2:
<p>
<XMP>
    $$hashref{"KEY"}   = "VALUE";       # CASE 0
    ${$hashref}{"KEY"} = "VALUE";       # CASE 1
    ${$hashref{"KEY"}} = "VALUE";       # CASE 2
    ${$hashref->{"KEY"}} = "VALUE";     # CASE 3

</XMP>
<p>Case 2 is also deceptive in that you're accessing a variable
called <STRONG>%hashref</STRONG>, not dereferencing through <STRONG>$hashref</STRONG> to the hash
it's presumably referencing.  That would be case 3.
<p>
<LI>The case of individual array elements arises often enough that it gets
cumbersome to use method 2.  As a form of syntactic sugar, the two
lines like that above can be written:
<p>
<XMP>
    $arrayref->[0] = "January";
    $hashref->{"KEY"} = "VALUE";

</XMP>
<p>The left side of the array can be any expression returning a reference,
including a previous dereference.  Note that <CODE><STRONG>$array</STRONG>[<STRONG>$x</STRONG>]</CODE> is <EM>NOT</EM> the
same thing as <CODE><STRONG>$array</STRONG>-&gt;[<STRONG>$x</STRONG>]</CODE> here:
<p>
<XMP>
    $array[$x]->{"foo"}->[0] = "January";

</XMP>
<p>This is one of the cases we mentioned earlier in which references could
spring into existence when in an lvalue context.  Before this
statement, <CODE><STRONG>$array</STRONG>[<STRONG>$x</STRONG>]</CODE> may have been undefined.  If so, it's
automatically defined with a hash reference so that we can look up
<CODE>{``foo''}</CODE> in it.  Likewise <CODE><STRONG>$array</STRONG>[<STRONG>$x</STRONG>]-&gt;{``foo''}</CODE> will automatically get
defined with an array reference so that we can look up <CODE>[0]</CODE> in it.
<p>One more thing here.  The arrow is optional <EM>BETWEEN</EM> brackets
subscripts, so you can shrink the above down to
<p>
<XMP>
    $array[$x]{"foo"}[0] = "January";

</XMP>
<p>Which, in the degenerate case of using only ordinary arrays, gives you
multidimensional arrays just like C's:
<p>
<XMP>
    $score[$x][$y][$z] += 42;

</XMP>
<p>Well, okay, not entirely like C's arrays, actually.  C doesn't know how
to grow its arrays on demand.  Perl does.
<p>
<LI>If a reference happens to be a reference to an object, then there are
probably methods to access the things referred to, and you should probably
stick to those methods unless you're in the class package that defines the
object's methods.  In other words, be nice, and don't violate the object's
encapsulation without a very good reason.  Perl does not enforce
encapsulation.  We are not totalitarians here.  We do expect some basic
civility though.
<p>
</OL>
The <EM>ref()</EM> operator may be used to determine what type of thing thereference is pointing to.  See the <EM>perlfunc</EM>  manpage
.
<p>The <EM>bless()</EM> operator may be used to associate a reference with a package
functioning as an object class.  See the <EM>perlobj</EM>  manpage
.
<p>A typeglob may be dereferenced the same way a reference can, since
the dereference syntax always indicates the kind of reference desired.
So <CODE>${*foo}</CODE> and <CODE>${\<STRONG>$foo</STRONG>}</CODE> both indicate the same scalar variable.
<p>Here's a trick for interpolating a subroutine call into a string:
<p>
<XMP>
    print "My sub returned @{[mysub(1,2,3)]} that time.\n";

</XMP>
<p>The way it works is that when the <CODE>@{...}</CODE> is seen in the double-quoted
string, it's evaluated as a block.  The block creates a reference to an
anonymous array containing the results of the call to <CODE>mysub(1,2,3)</CODE>.  So
the whole block returns a reference to an array, which is then
dereferenced by <CODE>@{...}</CODE> and stuck into the double-quoted string. This
chicanery is also useful for arbitrary expressions:
<p>
<XMP>
    print "That yields @{[$n + 5]} widgets\n";

</XMP>
<p>
<H2> 
<A NAME="perlref_symbolic_0">
Symbolic references</A>
</H2>
We said that references spring into existence as necessary if they are
undefined, but we didn't say what happens if a value used as a
reference is already defined, but <EM>ISN'T</EM> a hard reference.  If you
use it as a reference in this case, it'll be treated as a symbolic
reference.  That is, the value of the scalar is taken to be the 
<A HREF="perlref.html#perlref_name_0">NAME</A>

of a variable, rather than a direct link to a (possibly) anonymous
value.
<p>People frequently expect it to work like this.  So it does.
<p>
<XMP>
    $name = "foo";
    $$name = 1;                 # Sets $foo
    ${$name} = 2;               # Sets $foo
    ${$name x 2} = 3;           # Sets $foofoo
    $name->[0] = 4;             # Sets $foo[0]
    @$name = ();                # Clears @foo
    &$name();                   # Calls &foo() (as in Perl 4)
    $pack = "THAT";
    ${"${pack}::$name"} = 5;    # Sets $THAT::foo without eval

</XMP>
<p>This is very powerful, and slightly dangerous, in that it's possible
to intend (with the utmost sincerity) to use a hard reference, and
accidentally use a symbolic reference instead.  To protect against
that, you can say
<p>
<XMP>
    use strict 'refs';

</XMP>
<p>and then only hard references will be allowed for the rest of the enclosing
block.  An inner block may countermand that with 
<p>
<XMP>
    no strict 'refs';

</XMP>
<p>Only package variables are visible to symbolic references.  Lexical
variables (declared with <EM>my()</EM>) aren't in a symbol table, and thus are
invisible to this mechanism.  For example:
<p>
<XMP>
    local($value) = 10;
    $ref = \$value;
    {
        my $value = 20;
        print $$ref;
    } 

</XMP>
<p>This will still print 10, not 20.  Remember that <EM>local()</EM> affects package
variables, which are all ``global'' to the package.
<p>
<H2> 
<A NAME="perlref_not_so_symbolic_0">
Not-so-symbolic references</A>
</H2>
A new feature contributing to readability in 5.001 is that the brackets
around a symbolic reference behave more like quotes, just as they
always have within a string.  That is,
<p>
<XMP>
    $push = "pop on ";
    print "${push}over";

</XMP>
<p>has always meant to print ``pop on over'', despite the fact that push is
a reserved word.  This has been generalized to work the same outside
of quotes, so that
<p>
<XMP>
    print ${push} . "over";

</XMP>
<p>and even
<p>
<XMP>
    print ${ push } . "over";

</XMP>
<p>will have the same effect.  (This would have been a syntax error in
5.000, though Perl 4 allowed it in the spaceless form.)  Note that this
construct is <EM>not</EM> considered to be a symbolic reference when you're
using strict refs:
<p>
<XMP>
    use strict 'refs';
    ${ bareword };      # Okay, means $bareword.
    ${ "bareword" };    # Error, symbolic reference.

</XMP>
<p>Similarly, because of all the subscripting that is done using single
words, we've applied the same rule to any bareword that is used for
subscripting a hash.  So now, instead of writing
<p>
<XMP>
    $array{ "aaa" }{ "bbb" }{ "ccc" }

</XMP>
<p>you can just write
<p>
<XMP>
    $array{ aaa }{ bbb }{ ccc }

</XMP>
<p>and not worry about whether the subscripts are reserved words.  In the
rare event that you do wish to do something like
<p>
<XMP>
    $array{ shift }

</XMP>
<p>you can force interpretation as a reserved word by adding anything that
makes it more than a bareword:
<p>
<XMP>
    $array{ shift() }
    $array{ +shift }
    $array{ shift @_ }

</XMP>
<p>The <STRONG>-w</STRONG> switch will warn you if it interprets a reserved word as a string.
But it will no longer warn you about using lowercase words, since the
string is effectively quoted.
<p><p><hr>

<H1> 
<A NAME="perlref_warning_0">
WARNING</A>
</H1>
You may not (usefully) use a reference as the key to a hash.  It will be
converted into a string:
<p>
<XMP>
    $x{ \$a } = $a;

</XMP>
<p>If you try to dereference the key, it won't do a hard dereference, and 
you won't accomplish what you're attempting.  You might want to do something
more like
<p>
<XMP>
    $r = \@a;
    $x{ $r } = $r;

</XMP>
<p>And then at least you can use the <EM>values()</EM>, which will be
real refs, instead of the <EM>keys()</EM>, which won't.
<p><p><hr>

<H1> 
<A NAME="perlref_see_0">
SEE ALSO</A>
</H1>
Besides the obvious documents, source code can be instructive.
Some rather pathological examples of the use of references can be found
in the <EM>t/op/ref.t</EM> regression test in the Perl source directory.
<p>See also the <EM>perldsc</EM>  manpage
 and the <EM>perllol</EM>  manpage
 for how to use references to create
complex data structures, and the <EM>perlobj</EM>  manpage
 for how to use them to create
objects.
<p>
</BODY>
</HTML>
