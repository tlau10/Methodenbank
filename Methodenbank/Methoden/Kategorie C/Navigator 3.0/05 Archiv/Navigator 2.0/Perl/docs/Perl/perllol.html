<!-- $Id$ -->
<HTML><HEAD>
<CENTER><TITLE>perllol</TITLE>
</HEAD>
<BODY></CENTER><p><hr>

<H1> 
<A NAME="perllol_name_0">
NAME</A>
</H1>
perlLoL - Manipulating Lists of Lists in Perl
<p><p><hr>

<H1> 
<A NAME="perllol_description_0">
DESCRIPTION</A>
</H1>
<p><p><hr>

<H1> 
<A NAME="perllol_declaration_0">
Declaration and Access of Lists of Lists</A>
</H1>
The simplest thing to build is a list of lists (sometimes called an array
of arrays).  It's reasonably easy to understand, and almost everything
that applies here will also be applicable later on with the fancier data
structures.
<p>A list of lists, or an array of an array if you would, is just a regular
old array <STRONG>@LoL</STRONG> that you can get at with two subscripts, like <CODE><STRONG>$LoL</STRONG>[3][2]</CODE>.  Here's
a declaration of the array:
<p>
<XMP>
    # assign to our array a list of list references
    @LoL = ( 
           [ "fred", "barney" ],
           [ "george", "jane", "elroy" ],
           [ "homer", "marge", "bart" ],
    );
    print $LoL[2][2];
  bart

</XMP>
<p>Now you should be very careful that the outer bracket type
is a round one, that is, parentheses.  That's because you're assigning to
an <STRONG>@list</STRONG>, so you need parens.  If you wanted there <EM>not</EM> to be an <STRONG>@LoL</STRONG>,
but rather just a reference to it, you could do something more like this:
<p>
<XMP>
    # assign a reference to list of list references
    $ref_to_LoL = [
        [ "fred", "barney", "pebbles", "bambam", "dino", ],
        [ "homer", "bart", "marge", "maggie", ],
        [ "george", "jane", "alroy", "judy", ],
    ];
    print $ref_to_LoL->[2][2];

</XMP>
<p>Notice that the outer bracket type has changed, and so our access syntax 
has also changed.  That's because unlike C, in perl you can't freely
interchange arrays and references thereto.  <STRONG>$ref_to_LoL</STRONG> is a reference to an 
array, whereas <STRONG>@LoL</STRONG> is an array proper.  Likewise, <CODE><STRONG>$LoL</STRONG>[2]</CODE> is not an 
array, but an array ref.  So how come you can write these:
<p>
<XMP>
    $LoL[2][2]
    $ref_to_LoL->[2][2]

</XMP>
<p>instead of having to write these:
<p>
<XMP>
    $LoL[2]->[2]
    $ref_to_LoL->[2]->[2]

</XMP>
<p>Well, that's because the rule is that on adjacent brackets only (whether
square or curly), you are free to omit the pointer dereferencing arrow.
But you cannot do so for the very first one if it's a scalar containing
a reference, which means that <STRONG>$ref_to_LoL</STRONG> always needs it.
<p><p><hr>

<H1> 
<A NAME="perllol_growing_0">
Growing Your Own</A>
</H1>
That's all well and good for declaration of a fixed data structure,
but what if you wanted to add new elements on the fly, or build
it up entirely from scratch?
<p>First, let's look at reading it in from a file.  This is something like
adding a row at a time.  We'll assume that there's a flat file in which
each line is a row and each word an element.  If you're trying to develop an
<STRONG>@LoL</STRONG> list containing all these, here's the right way to do that:
<p>
<XMP>
    while (<>) {
        @tmp = split;
        push @LoL, [ @tmp ];
    } 

</XMP>
<p>You might also have loaded that from a function:
<p>
<XMP>
    for $i ( 1 .. 10 ) {
        $LoL[$i] = [ somefunc($i) ];
    }

</XMP>
<p>Or you might have had a temporary variable sitting around with the
list in it.  
<p>
<XMP>
    for $i ( 1 .. 10 ) {
        @tmp = somefunc($i);
        $LoL[$i] = [ @tmp ];
    }

</XMP>
<p>It's very important that you make sure to use the <CODE>[]</CODE> list reference
constructor.  That's because this will be very wrong:
<p>
<XMP>
    $LoL[$i] = @tmp;

</XMP>
<p>You see, assigning a named list like that to a scalar just counts the 
number of elements in <STRONG>@tmp</STRONG>, which probably isn't what you want.  
<p>If you are running under <CODE>use strict</CODE>, you'll have to add some
declarations to make it happy:
<p>
<XMP>
    use strict;
    my(@LoL, @tmp);
    while (<>) {
        @tmp = split;
        push @LoL, [ @tmp ];
    } 

</XMP>
<p>Of course, you don't need the temporary array to have a name at all:
<p>
<XMP>
    while (<>) {
        push @LoL, [ split ];
    } 

</XMP>
<p>You also don't have to use <EM>push()</EM>.  You could just make a direct assignment
if you knew where you wanted to put it:
<p>
<XMP>
    my (@LoL, $i, $line);
    for $i ( 0 .. 10 ) {
        $line = <>;
        $LoL[$i] = [ split ' ', $line ];
    } 

</XMP>
<p>or even just
<p>
<XMP>
    my (@LoL, $i);
    for $i ( 0 .. 10 ) {
        $LoL[$i] = [ split ' ', <> ];
    } 

</XMP>
<p>You should in general be leery of using potential list functions
in a scalar context without explicitly stating such.  
This would be clearer to the casual reader:
<p>
<XMP>
    my (@LoL, $i);
    for $i ( 0 .. 10 ) {
        $LoL[$i] = [ split ' ', scalar(<>) ];
    } 

</XMP>
<p>If you wanted to have a <STRONG>$ref_to_LoL</STRONG> variable as a reference to an array,
you'd have to do something like this:
<p>
<XMP>
    while (<>) {
        push @$ref_to_LoL, [ split ];
    } 

</XMP>
<p>Actually, if you were using strict, you'd not only have to declare <STRONG>$ref_to_LoL</STRONG> as
you had to declare <STRONG>@LoL</STRONG>, but you'd <EM>also</EM> having to initialize it to a
reference to an empty list.  (This was a bug in 5.001m that's been fixed
for the 5.002 release.)
<p>
<XMP>
    my $ref_to_LoL = [];
    while (<>) {
        push @$ref_to_LoL, [ split ];
    } 

</XMP>
<p>Ok, now you can add new rows.  What about adding new columns?  If you're
just dealing with matrices, it's often easiest to use simple assignment:
<p>
<XMP>
    for $x (1 .. 10) {
        for $y (1 .. 10) {
            $LoL[$x][$y] = func($x, $y);
        }
    }
    for $x ( 3, 7, 9 ) {
        $LoL[$x][20] += func2($x);
    } 

</XMP>
<p>It doesn't matter whether those elements are already 
there or not: it'll gladly create them for you, setting
intervening elements to <CODE>undef</CODE> as need be.
<p>If you just wanted to append to a row, you'd have
to do something a bit funnier looking:
<p>
<XMP>
    # add new columns to an existing row
    push @{ $LoL[0] }, "wilma", "betty";

</XMP>
<p>Notice that I <EM>couldn't</EM> just say:
<p>
<XMP>
    push $LoL[0], "wilma", "betty";  # WRONG!

</XMP>
<p>In fact, that wouldn't even compile.  How come?  Because the argument
to <EM>push()</EM> must be a real array, not just a reference to such.
<p><p><hr>

<H1> 
<A NAME="perllol_access_0">
Access and Printing</A>
</H1>
Now it's time to print your data structure out.  How 
are you going to do that?  Well, if you only want one
of the elements, it's trivial:
<p>
<XMP>
    print $LoL[0][0];

</XMP>
<p>If you want to print the whole thing, though, you can't
just say 
<p>
<XMP>
    print @LoL;         # WRONG

</XMP>
<p>because you'll just get references listed, and perl will never
automatically dereference things for you.  Instead, you have to 
roll yourself a loop or two.  This prints the whole structure,
using the shell-style <EM>for()</EM> construct to loop across the outer
set of subscripts.  
<p>
<XMP>
    for $aref ( @LoL ) {
        print "\t [ @$aref ],\n";
    }

</XMP>
<p>If you wanted to keep track of subscripts, you might do this:
<p>
<XMP>
    for $i ( 0 .. $#LoL ) {
        print "\t elt $i is [ @{$LoL[$i]} ],\n";
    }

</XMP>
<p>or maybe even this.  Notice the inner loop.
<p>
<XMP>
    for $i ( 0 .. $#LoL ) {
        for $j ( 0 .. $#{$LoL[$i]} ) {
            print "elt $i $j is $LoL[$i][$j]\n";
        }
    }

</XMP>
<p>As you can see, it's getting a bit complicated.  That's why 
sometimes is easier to take a temporary on your way through:
<p>
<XMP>
    for $i ( 0 .. $#LoL ) {
        $aref = $LoL[$i];
        for $j ( 0 .. $#{$aref} ) {
            print "elt $i $j is $LoL[$i][$j]\n";
        }
    }

</XMP>
<p>Hm... that's still a bit ugly.  How about this:
<p>
<XMP>
    for $i ( 0 .. $#LoL ) {
        $aref = $LoL[$i];
        $n = @$aref - 1;
        for $j ( 0 .. $n ) {
            print "elt $i $j is $LoL[$i][$j]\n";
        }
    }

</XMP>
<p><p><hr>

<H1> 
<A NAME="perllol_slices_0">
Slices</A>
</H1>
If you want to get at a slice (part of a row) in a multidimensional
array, you're going to have to do some fancy subscripting.  That's
because while we have a nice synonym for single elements via the
pointer arrow for dereferencing, no such convenience exists for slices.
(Remember, of course, that you can always write a loop to do a slice
operation.)
<p>Here's how to do one operation using a loop.  We'll assume an <STRONG>@LoL</STRONG>
variable as before.
<p>
<XMP>
    @part = ();
    $x = 4;     
    for ($y = 7; $y < 13; $y++) {
        push @part, $LoL[$x][$y];
    } 

</XMP>
<p>That same loop could be replaced with a slice operation:
<p>
<XMP>
    @part = @{ $LoL[4] } [ 7..12 ];

</XMP>
<p>but as you might well imagine, this is pretty rough on the reader.
<p>Ah, but what if you wanted a <EM>two-dimensional slice</EM>, such as having
<STRONG>$x</STRONG> run from 4..8 and <STRONG>$y</STRONG> run from 7 to 12?  Hm... here's the simple way:
<p>
<XMP>
    @newLoL = ();
    for ($startx = $x = 4; $x <= 8; $x++) {
        for ($starty = $y = 7; $x <= 12; $y++) {
            $newLoL[$x - $startx][$y - $starty] = $LoL[$x][$y];
        }
    } 

</XMP>
<p>We can reduce some of the looping through slices 
<p>
<XMP>
    for ($x = 4; $x <= 8; $x++) {
        push @newLoL, [ @{ $LoL[$x] } [ 7..12 ] ];
    }

</XMP>
<p>If you were into Schwartzian Transforms, you would probably
have selected map for that
<p>
<XMP>
    @newLoL = map { [ @{ $LoL[$_] } [ 7..12 ] ] } 4 .. 8;

</XMP>
<p>Although if your manager accused of seeking job security (or rapid
insecurity) through inscrutable code, it would be hard to argue. :-)
If I were you, I'd put that in a function:
<p>
<XMP>
    @newLoL = splice_2D( \@LoL, 4 => 8, 7 => 12 );
    sub splice_2D {
        my $lrr = shift;        # ref to list of list refs!
        my ($x_lo, $x_hi, 
            $y_lo, $y_hi) = @_;
        return map { 
            [ @{ $lrr->[$_] } [ $y_lo .. $y_hi ] ] 
        } $x_lo .. $x_hi;
    } 

</XMP>
<p><p><hr>

<H1> 
<A NAME="perllol_see_0">
SEE ALSO</A>
</H1>
perldata(1), perlref(1), perldsc(1)
<p><p><hr>

<H1> 
<A NAME="perllol_author_0">
AUTHOR</A>
</H1>
Tom Christiansen &lt;<EM><A HREF="MAILTO:tchrist@perl.com">tchrist@perl.com</A></EM>&gt;
<p>Last udpate: Sat Oct  7 19:35:26 MDT 1995
<p>
</BODY>
</HTML>
